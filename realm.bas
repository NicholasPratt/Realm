   10 MODE 0
   20 VDU 23,1,0
   30 VDU 23,0,&C0,1
   40 PROC_INITVARS
   50 LEVEL%=0
   60 DEVMODE%=0
   70 PROC_DEVELOPMENTMODE
   80 DIM LEVEL$(2)
   90 LEVEL$(0)="GOATFARM.TXT"
  100 LEVEL$(1)="UNDERYON.TXT"
  110 LEVEL$(2)="THECASTLE.TXT"
  120 CLS
  130 PROC_CUSTOMCHAR
  140 PROC_POPMAP
  150 PROC_DRAWMAP
  160 PROC_POPMONSTER
  170 PROC_DRAWMONSTER
  180 PROC_DRAWBOX
  190 REPEAT
  200   T%=50 : FALLING%=0
  210   PMOVY%=0 : PMOVX%=0
  220   OX%=PX% : OY%=PY%
  230   IF LEVIOSA%>0 THEN LEVIOSA%=LEVIOSA%-1 : PRINTTAB(20,24)"You feel lighter than air.": GOTO 320
  240   COLOUR128:PRINTTAB(20,24)"                           "
  250   IF MAP%(PX%,PY%+1,CURSCREE%)=32 THEN PY%=PY%+1 :T%=5 :FALLING%=1
  260   IF MAP%(PX%,PY%+1,CURSCREE%)=34 THEN PY%=PY%+1 :T%=5 :FALLING%=1
  270   IF MAP%(PX%,PY%+1,CURSCREE%)=46 THEN PY%=PY%+1 :T%=5 :FALLING%=1
  280   IF MAP%(PX%,PY%+1,CURSCREE%)=96 THEN PY%=PY%+1 :T%=5 :FALLING%=1
  290   IF MAP%(PX%,PY%+1,CURSCREE%)=45 THEN PROC_REVEALHID
  300   IF MAP%(PX%,PY%+1,CURSCREE%)=45 THEN MAP%(PX%,PY%+1,CURSCREE%)=33
  310   IF MAP%(PX%,PY%+1,CURSCREE%)=38 AND HID%(CURSCREE%)=1 THEN PY%=PY%+1 : T%=5:FALLING%=1
  320   A%=INKEY(T%)
  330   IF A%=11 THEN PROC_MOVEU
  340   IF A%=10 THEN PROC_MOVED
  350   IF A%=8 THEN PROC_MOVEL
  360   IF A%=21 THEN PROC_MOVER
  370   PX%=PX%+PMOVX% : PY%=PY%+PMOVY%
  380   IF A%=80 THEN PROC_HEALPOT
  390   IF A%=87 THEN PROC_USEWAND
  400   COLOUR 15:COLOUR 128: PRINT TAB(PX%,PY%) CHR$(PDIREC%)
  410   IF OX%<>PX% OR OY%<>PY% THEN PROC_FIXMAP
  420   PROC_PRINTSTATS
  430   IF MAP%(PX%,PY%,CURSCREE%)=123 THEN SCREEX%=SCREEX%-1 : PX%=54 : PROC_NEXTSCREEN
  440   IF MAP%(PX%,PY%,CURSCREE%)=125 THEN SCREEX%=SCREEX%+1 : PX%=2:PROC_NEXTSCREEN
  450   IF MAP%(PX%,PY%,CURSCREE%)=126 THEN SCREEY%=SCREEY%-1 : PROC_NEXTSCREEN
  460   IF FALLING%=1 AND PY%=15 THEN PROC_FALLSCREE
  470   IF MAP%(PX%,PY%,CURSCREE%)=124 THEN SCREEY%=SCREEY%+1 : PROC_NEXTSCREEN
  480   IF MAP%(PX%,PY%,CURSCREE%)=63 THEN PROC_NEWLEVEL
  490   PROC_CHECKENEMY
  500   IF OX%<>PX% OR OY%<>PY% THEN PROC_FIXMAP
  510   PROC_CHECKITEM
  520   IF MAP%(PX%,PY%+1,CURSCREE%)=95 THEN DEATH%=3
  530 UNTIL DEATH%<>0
  540 PROC_DEATH(DEATH%)
  550 END
  560 DEF PROC_DEVELOPMENTMODE
  570 PROC_REALM3D
  580 MOVE 30,40 : DRAW 30,1000 : DRAW 1250,1000 : DRAW 1250,40 : DRAW 30,40
  590 PRINTTAB(14,51)"Nicholas Pratt"
  600 PRINTTAB(14,53)"December 2023" 
  601 PRINTTAB(6,41)"Buy me a coffee!"
  602 PRINTTAB(6,43)"ko-fi.com/nicholaspratt"
  603 PRINTTAB(31,4)"***BETA VERSION***"
  610 COLOUR RND(14):PRINTTAB(26,24)"[N]new game     [L]load saved"
  620 COLOUR RND(14):PRINTTAB(12,30)" Welcome to the Realm."
  630 COLOUR RND(14):PRINTTAB(12,32)"A fantasy world inside your Agon Light computer."
  640 COLOUR RND(14):PRINTTAB(12,34)" Use the arrow keys to move the hero."
  650 COLOUR RND(14):PRINTTAB(12,36)"Ensure that CAPSLOCK is on."
  660 A$=GET$
  670 REM IF A$="L" OR A$="l" THEN PROC_LOADDEV
  680 REM IF A$="N" OR A$="n" THEN PROC_DEVSET ELSE GOTO 620
  690 IF A$="L" OR A$="l" THEN PROC_LOADGAME : GOTO 720
  700 REM SPAWNPOINT%=1
  710 REM DEVMODE%=1
  720 ENDPROC
  730 DEF PROC_DEVSET
  740 DEV=OPENOUT "DEVSETTINGS.TXT"
  750 PRINT "CHOOSE LEVEL":INPUTA$:A%=VAL(A$):LEVEL%=A%-1:PRINT#DEV,A$
  760 PRINT "CURSCREE" :INPUTA$:A%=VAL(A$):CURSCREE%=A%-1:PRINT#DEV,A$
  770 PRINT"PX":INPUTA$:A%=VAL(A$):PX%=A%:PRINT#DEV,A$
  780 PRINT"PY":INPUTA$:A%=VAL(A$):PY%=A%:PRINT#DEV,A$
  790 FOR I%=0TO3
  800   PRINT"KEY";(I%+1);"Y or N":INPUTA$
  810   IF A$="Y" OR A$="y" THEN PRINT#DEV,"1":KEY%(I%)=1 ELSE PRINT#DEV,"0":KEY%(I%)=0
  820 NEXT
  830 CLOSE#DEV
  840 ENDPROC
  850 DEF PROC_FALLSCREE
  860 IF MAP%(PX%,PY%+1,CURSCREE%)=124  THEN SCREEY%=SCREEY%+1 : PROC_NEXTSCREEN
  870 ENDPROC
  880 DEF PROC_LOADDEV
  890 DEV=OPENIN "DEVSETTINGS.TXT"
  900 INPUT#DEV,A$ :A%=VAL(A$):LEVEL%=A%-1
  910 INPUT#DEV,A$ :A%=VAL(A$):CURSCREE%=A%-1
  920 INPUT#DEV,A$ :A%=VAL(A$):PX%=A%
  930 INPUT#DEV,A$ :A%=VAL(A$):PY%=A%
  940 FOR I%=0TO3
  950   INPUT#DEV,A$ :A%=VAL(A$):KEY%(I%)=A%
  960 NEXT
  970 CLOSE#DEV
  980 ENDPROC
  990 DEF PROC_NEWLEVEL
 1000 CLS
 1010 PRINTTAB(3,3)"PRESS [Y] TO SAVE GAME":A$=GET$
 1020 IF CURSCREE%=4 THEN LEVEL%=LEVEL%+1 : CURSCREE%=0  GOTO 1090
 1030 IF CURSCREE%=0 THEN LEVEL%=LEVEL%-1 : CURSCREE%=4
 1040 FOR I%=0TO4
 1050   FOR J%=0TO4
 1060     IF SCREENS%(J%,I%)=4 AND CURSCREE%=4 THEN SCREEX%=I% : SCREEY%=J%
 1070   NEXT
 1080 NEXT 
 1090 IF A$="Y" THEN PROC_SAVEGAME
 1100 CLS
 1110 SPAWNPOINT%=0
 1120 FOR I%=0TO3
 1130   KEY%(I%)=0
 1140 NEXT
 1150 FOR I%=0TO24
 1160   FOR J%=0TO8
 1170     MONSTERS%(I%,J%)=0
 1180   NEXT
 1190 NEXT
 1200 PROC_POPMAP
 1210 PROC_DRAWMAP
 1220 PROC_POPMONSTER
 1230 PROC_DRAWMONSTER
 1240 PROC_DRAWBOX
 1250 ENDPROC
 1260 DEF PROC_SAVEGAME
 1270 PRINTTAB(3,5)"SAVING...."
 1280 SAVG=OPENOUT "SAVEGAME.TXT"
 1290 LEVEL$=STR$(LEVEL%):PRINT#SAVG,LEVEL$
 1300 CURSCREE$=STR$(CURSCREE%):PRINT#SAVG,CURSCREE$
 1310 MKILLS$=STR$(MKILLS%) :PRINT#SAVG,MKILLS$
 1320 GOLD$=STR$(GOLD%) : PRINT#SAVG,GOLD$
 1330 QUESTS$=STR$(QUESTS%) : PRINT#SAVG,QUESTS$
 1340 FOR I%=0 TO 2
 1350   PSTAT$=STR$(PSTAT%(I%)) :PRINT#SAVG,PSTAT$
 1360 NEXT
 1370 FOR J%=0TO3
 1380   FOR I%=0TO2
 1390     PPOTS$=STR$(PPOTS%(J%,I%)) : PRINT#SAVG,PPOTS$
 1400   NEXT
 1410 NEXT
 1420 SWORD$=STR$(SWORD%) : PRINT#SAVG,SWORD$
 1430 FOR I%=0TO7
 1440   PRINT#SAVG,PLITEMS$(I%)
 1450 NEXT
 1460 FOR J%=0TO7
 1470   FOR I%=0TO1
 1480     PRWND$=STR$(PWANDS%(J%,I%)) : PRINT#SAVG,PRWND$
 1490   NEXT
 1500 NEXT
 1510 CLOSE#SAVG
 1520 CLS
 1530 ENDPROC
 1540 DEF PROC_LOADGAME
 1550 PRINTTAB(3,5)"LOADING...." :A%=INKEY(300)
 1560 SAVG=OPENIN "SAVEGAME.TXT"
 1570 INPUT#SAVG,LEVEL$ : LEVEL%=VAL(LEVEL$)
 1580 INPUT#SAVG,CURSCREE$ : CURSCREE%=VAL(CURSCREE$)
 1590 INPUT#SAVG,MKILLS$ : MKILLS%=VAL(MKILLS$)
 1600 INPUT#SAVG,GOLD$ : GOLD%=VAL(GOLD$)
 1610 INPUT#SAVG,QUESTS$ : QUESTS%=VAL(QUESTS$)
 1620 FOR I%=0 TO 2
 1630   INPUT#SAVG,PSTAT$ : PSTAT%(I%)=VAL(PSTAT$)
 1640 NEXT
 1650 FOR J%=0TO3
 1660   FOR I%=0TO2
 1670     INPUT#SAVG,PPOTS$ :  PPOTS%(J%,I%)=VAL(PPOTS$)
 1680   NEXT
 1690 NEXT
 1700 INPUT#SAVG,SWORD$ : SWORD%=VAL(SWORD$)
 1710 FOR I%=0TO7
 1720   INPUT#SAVG,PLITEMS$(I%)
 1730 NEXT
 1740 FOR J%=0TO7
 1750   FOR I%=0TO1
 1760     INPUT#SAVG,PRWND$ : PWANDS%(J%,I%)=VAL(PRWND$)
 1770   NEXT
 1780 NEXT
 1790 CLOSE#SAVG
 1800 CLS
 1810 ENDPROC
 1820 DEF PROC_INITVARS
 1830 DIM ITEMS%(25,7)
 1840 DIM MAP%(57,16,4)
 1850 DIM THEYMAPS%(2)
 1860 OX%=10 : OY%=10
 1870 PX%=0 : PY%=0
 1880 PDIREC%=234
 1890 DIM MONSTERS%(24,8)
 1900 DIM MONSNAME$(24)
 1910 DIM PSTAT%(2)
 1920 PSTAT%(0)=5 : PSTAT%(1)=2 : PSTAT%(2)=1
 1930 MAXH%=PSTAT%(0) : MAXA%=PSTAT%(1) : MAXS%=PSTAT%(2)
 1940 GDIREC%=243
 1950 DIM SCREENS%(4,4)
 1960 DIM TLINE1$(4)
 1970 DIM TLINE2$(4)
 1980 SCREEX%=0 : SCREEY%=0
 1990 CURSCREE%=SCREENS%(SCREEY%,SCREEX%)
 2000 GOLD%=0
 2010 DIM PPOTS%(3,2)
 2020 DIM KEY%(3)
 2030 SWORD%=0
 2040 DIM NPCSPEAK$(19,5)
 2050 DIM PLITEMS$(7)
 2060 DIM CHESTITEM$(9)
 2070 DIM NPCITEMS$(9)
 2080 DIM PWANDS%(7,1)
 2090 DIM WAND$(6)
 2100 WAND$(1)="ICE BLAST ":WAND$(2)="FIRE STORM":WAND$(3)="EARTHQUAKE":WAND$(4)="AIR STRIKE":WAND$(5)="TELEPORT  ":WAND$(6)="LEVITATION"
 2110 WAND$(0)="          "
 2120 DEATH%=0
 2130 MKILLS%=0 
 2140 QUESTS%=0 
 2150 LEVIOSA%=0
 2160 DIM HID%(4)
 2170 SPAWNPOINT%=0
 2180 ENDPROC
 2190 DEF PROC_CUSTOMCHAR
 2200 VDU 23,230,255,255,255,255,255,255,255,255  : REM BLOCK
 2210 VDU 23,231,255,145,255,197,255,145,255,255  : REM WALL
 2220 VDU 23,232,120,81,17,57,85,83,41,104 : REM LPLAYA
 2230 VDU 23,233,60,36,60,36,60,36,60,36        : REM LADDER
 2240 VDU 23,234,30,138,136,156,170,202,140,22 : REM RPLAYA
 2250 VDU 23,235,120,80,16,60,254,80,40,104 : REM FLPLAYA
 2260 VDU 23,236,30,10,8,60,127,10,20,22   : REM FRPLAYA
 2270 VDU 23,237,3,2,15,9,63,36,255,219        : REM LSTAIR
 2280 VDU 23,238,192,64,240,144,252,26,255,219         : REM RSTAIR
 2290 VDU 23,239,5,6,7,8,16,32,64,0         : REM WAND
 2300 VDU 23,240,24,126,255,171,255,170,255,171 : REM DOOR
 2310 VDU 23,241,126,133,135,253,133,133,134,252 : REM CHEST
 2320 VDU 23,244,0,0,64,160,191,163,64,0 : REM KEY
 2330 VDU 23,245,0,0,16,32,124,32,16,0   : REM LEFT ARROW
 2340 VDU 23,246,0,0,16,16,84,56,16,0    : REM DOWN
 2350 VDU 23,247,0,0,8,4,62,4,8,0        : REM RIGHT
 2360 VDU 23,248,0,8,28,42,8,8,0,0       : REM UP
 2370 VDU 23,249,8,42,20,20,34,65,65,62  : REM POTION
 2380 VDU 23,250,0,2,4,72,48,48,72,0     : REM SWORD
 2390 VDU 23,251,60,66,145,137,253,9,18,4 : REM LEVEL
 2400 VDU 23,252,0,85,85,85,85,85,85,255  : REM SPIKES
 2410 ENDPROC
 2420 DEF PROC_DRAWBOX
 2430 GCOL 0,15
 2440 MOVE 250,660 :DRAW 1020,660 : DRAW 1020,430 : DRAW 250,430 : DRAW 250,660
 2450 GCOL 0,3
 2460 VDU 23,23,6;0;0;0;
 2470 RX%=275 : RY%=175
 2480 MOVE 20+RX%,10+RY% : DRAW 20+RX%,70+RY% : DRAW 47+RX%,65+RY% : DRAW 50+RX%,45+RY% : DRAW 35+RX%,30+RY% : DRAW 64+RX%,10+RY%
 2490 MOVE 115+RX%,12+RY% :DRAW 75+RX%,12+RY% : DRAW 75+RX%,72+RY% : DRAW 110+RX%,72+RY% : MOVE 75+RX%,48+RY% : DRAW 90+RX%,48+RY%
 2500 MOVE 125+RX%,13+RY% :DRAW 150+RX%,73+RY%:DRAW 175+RX%,13+RY% :MOVE 168+RX%,33+RY% : DRAW 148+RX%,33+RY%
 2510 MOVE 185+RX%,71+RY% : DRAW 185+RX%,11+RY%: DRAW 210+RX%,11+RY%
 2520 MOVE 220+RX%,13+RY% : DRAW 220+RX%,69+RY%: DRAW 239+RX%,49+RY% :DRAW 256+RX%,69+RY% : DRAW 256+RX%,12+RY%
 2530 ENDPROC
 2540 DEF PROC_POPMAP
 2550 COLOUR15:PRINTTAB(18,51)LEVEL$(LEVEL%)
 2560 TXT=OPENIN LEVEL$(LEVEL%)
 2570 FOR I%=0TO4
 2580   FOR J%=0TO4
 2590     A%=BGET#TXT
 2600     SCREENS%(J%,I%)=A%-49
 2610     IF SCREENS%(J%,I%)=0 AND DEVMODE%=0 THEN SCREEX%=J% :SCREEY%=I%
 2620     IF SCREENS%(J%,I%)=4 AND CURSCREE%=4 THEN SCREEX%=J%:SCREEY%=I% : DEVMODE%=0
 2630   NEXT
 2640 NEXT
 2650 PTR#TXT=PTR#TXT+2
 2660 FOR I%=0TO2
 2670   INPUT#TXT,A$
 2680   A%=VAL(A$)
 2690   PTR#TXT=PTR#TXT+1
 2700   THEYMAPS%(I%)=A%
 2710 NEXT
 2720 BGCOL%=THEYMAPS%(0) : BG%=THEYMAPS%(1)
 2730 WC%=THEYMAPS%(2)
 2740 FOR K%=0TO4
 2750   FOR J%=1 TO 16
 2760     FOR I%=1 TO 57
 2770       MAP%(I%,J%,K%)=BGET#TXT
 2780     NEXT
 2790   NEXT
 2800   R%=BGET#TXT
 2810   R%=R%-48
 2820   IF R%=0 THEN GOTO 2920
 2830   FOR I%=1TO R%
 2840     M%=BGET#TXT
 2850     A%=BGET#TXT
 2860     B%=BGET#TXT
 2870     C%=BGET#TXT
 2880     D%=BGET#TXT
 2890     M%=M%-65
 2900     ITEMS%(M%,3)=A%-48 :ITEMS%(M%,4)=B%-48 :ITEMS%(M%,5)=C%-48:ITEMS%(M%,6)=D%
 2910   NEXT
 2920   PTR#TXT=PTR#TXT+2
 2930   B%=BGET#TXT
 2940   B%=B%-48
 2950   IF B%=0 THEN :PTR#TXT=PTR#TXT+2: GOTO 3000
 2960   C%=BGET#TXT :C%=C%-48 : D%=BGET#TXT:D%=D%-48:PTR#TXT=PTR#TXT+2
 2970   FOR ITN%=C% TO D%
 2980     INPUT#TXT,CHESTITEM$(ITN%):PTR#TXT=PTR#TXT+1
 2990   NEXT
 3000   B%=BGET#TXT
 3010   PTR#TXT=PTR#TXT+2
 3020   B%=B%-48
 3030   IF B%=0 THEN GOTO 3210
 3040   FOR L%=1 TO B%
 3050     C%=BGET#TXT : D%=BGET#TXT : E%=BGET#TXT : F%=BGET#TXT : G%=BGET#TXT : H%=BGET#TXT
 3060     MONSTERS%(C%-97,0)=81+D%
 3070     MONSTERS%(C%-97,4)=E%-48:MONSTERS%(C%-97,5)=F%-48:MONSTERS%(C%-97,6)=G%-48
 3080     MONSTERS%(C%-97,7)=H%
 3090     PTR#TXT=PTR#TXT+2
 3100     INPUT#TXT,MONSNAME$(C%-96)
 3110     PTR#TXT=PTR#TXT+1
 3120     IF MONSTERS%(C%-97,5)=10 THEN HID%(K%)=MONSTERS%(C%-97,6)
 3130     IF D%+81<>138 GOTO 3200
 3140     INPUT#TXT,NPCNUMBER$ : PTR#TXT=PTR#TXT+1
 3150     NPCNUMBER%=VAL(NPCNUMBER$) : MONSTERS%(C%-97,8)=NPCNUMBER%
 3160     FOR FT%=0TO4
 3170       INPUT#TXT,NPCSPEAK$(E%-48,FT%) : PTR#TXT=PTR#TXT+1
 3180     NEXT
 3190     NPCITEMS$(MONSTERS%(C%-97,8))=NPCSPEAK$(E%-48,3)
 3200   NEXT
 3210   INPUT#TXT,TLINE1$(K%) : PTR#TXT=PTR#TXT+1
 3220   INPUT#TXT,TLINE2$(K%)
 3230   PTR#TXT=PTR#TXT+1
 3240 NEXT
 3250 K%=BGET#TXT : PTR#TXT=PTR#TXT+2
 3260 K%=K%-48
 3270 IF K%=0 THEN GOTO 3410
 3280 FOR I%=1TOK%
 3290   INPUT#TXT,Q$ : PTR#TXT=PTR#TXT+1 : Q%=VAL(Q$)
 3300   INPUT#TXT,A$ : PTR#TXT=PTR#TXT+1 : A%=VAL(A$)
 3310   INPUT#TXT,B$ : PTR#TXT=PTR#TXT+1 : B%=VAL(B$)
 3320   INPUT#TXT,C$ : PTR#TXT=PTR#TXT+1 : C%=VAL(C$)
 3330   INPUT#TXT,D$ : PTR#TXT=PTR#TXT+1 : D%=VAL(D$)
 3340   INPUT#TXT,E$ : PTR#TXT=PTR#TXT+1 : E%=VAL(E$)
 3350   INPUT#TXT,F$ : PTR#TXT=PTR#TXT+1 : F%=VAL(F$)
 3360   INPUT#TXT,G$ : PTR#TXT=PTR#TXT+1 : G%=VAL(G$)
 3370   INPUT#TXT,H$ : H%=VAL(H$)
 3380   PTR#TXT=PTR#TXT+1
 3390   VDU 23,Q%,A%,B%,C%,D%,E%,F%,G%,H%
 3400 NEXT
 3410 CLOSE#TXT
 3420 ENDPROC
 3430 DEF PROC_DRAWMAP
 3440 COLOUR15:PRINTTAB(18,51)LEVEL$(LEVEL%)
 3450 FOR J%=1TO16
 3460   FOR I%=1TO57
 3470     IF MAP%(I%,J%,CURSCREE%)=45 THENCOLOURWC%:COLOUR128+BGCOL%: PRINTTAB(I%,J%)CHR$(231):GOTO 3700
 3480     IF MAP%(I%,J%,CURSCREE%)=38 AND HID%(CURSCREE%)=0 THENCOLOURWC%:COLOUR128+BGCOL%: PRINTTAB(I%,J%)CHR$(231):GOTO 3700
 3490     IF MAP%(I%,J%,CURSCREE%)=64AND SPAWNPOINT%=0THEN PX%=I%:PY%=J%-1:MAP%(I%,J%,CURSCREE%)=33:SPAWNPOINT%=1:GOTO3570
 3500     IF MAP%(I%,J%,CURSCREE%)=64AND SPAWNPOINT%=1THEN:MAP%(I%,J%,CURSCREE%)=33:GOTO3570
 3510     IF MAP%(I%,J%,CURSCREE%)=38 AND HID%(CURSCREE%)=1 THEN COLOUR8:PRINTTAB(I%,J%)CHR$(230):GOTO 3700
 3520     IF MAP%(I%,J%,CURSCREE%)=63THENCOLOUR139:COLOUR9:PRINTTAB(I%,J%)CHR$(251):GOTO 3700
 3530     IF MAP%(I%,J%,CURSCREE%)=34THENCOLOURBG%:PRINTTAB(I%,J%)CHR$(230):GOTO 3700
 3540     IF MAP%(I%,J%,CURSCREE%)=36THENCOLOUR128+BGCOL%:COLOURWC%:PRINTTAB(I%,J%)CHR$(237):GOTO 3700
 3550     IF MAP%(I%,J%,CURSCREE%)=37THENCOLOUR128+BGCOL%:COLOURWC%:PRINTTAB(I%,J%)CHR$(238):GOTO 3700
 3560     IF MAP%(I%,J%,CURSCREE%)=46THENCOLOUR8:PRINTTAB(I%,J%)CHR$(230):GOTO 3700
 3570     IF MAP%(I%,J%,CURSCREE%)=33THENCOLOURWC%:COLOUR128+BGCOL%: PRINTTAB(I%,J%)CHR$(231):GOTO 3700
 3580     IF MAP%(I%,J%,CURSCREE%)=95THENCOLOURWC%:COLOUR128: PRINTTAB(I%,J%)CHR$(252):GOTO 3700
 3590     IF MAP%(I%,J%,CURSCREE%)=35THENCOLOUR128:COLOUR2:PRINTTAB(I%,J%)CHR$(233):GOTO 3700
 3600     IF MAP%(I%,J%,CURSCREE%)=123THENCOLOUR139:COLOUR0:PRINTTAB(I%,J%)CHR$(245):GOTO 3700
 3610     IF MAP%(I%,J%,CURSCREE%)=124THENCOLOUR139:COLOUR0:PRINTTAB(I%,J%)CHR$(246):GOTO 3700
 3620     IF MAP%(I%,J%,CURSCREE%)=125THENCOLOUR139:COLOUR0:PRINTTAB(I%,J%)CHR$(247):GOTO 3700
 3630     IF MAP%(I%,J%,CURSCREE%)=126THENCOLOUR139:COLOUR0:PRINTTAB(I%,J%)CHR$(248):GOTO 3700
 3640     IF MAP%(I%,J%,CURSCREE%)=91THENCOLOUR128:COLOUR10:PRINTTAB(I%,J%)CHR$(240):GOTO 3700
 3650     IF MAP%(I%,J%,CURSCREE%)=92THENCOLOUR128:COLOUR11:PRINTTAB(I%,J%)CHR$(240):GOTO 3700
 3660     IF MAP%(I%,J%,CURSCREE%)=93THENCOLOUR128:COLOUR12:PRINTTAB(I%,J%)CHR$(240):GOTO 3700
 3670     IF MAP%(I%,J%,CURSCREE%)=94THENCOLOUR128:COLOUR13:PRINTTAB(I%,J%)CHR$(240):GOTO 3700
 3680     IF MAP%(I%,J%,CURSCREE%)=96THENCOLOUR128:COLOUR15:PRINTTAB(I%,J%)CHR$(139):GOTO 3700
 3690     IF MAP%(I%,J%,CURSCREE%)>=65 AND MAP%(I%,J%,CURSCREE%)<=90 THEN PROC_DRAWITEMS
 3700   NEXT
 3710 NEXT
 3720 COLOUR 128
 3730 COLOUR 7 :PRINT TAB(1,18) TLINE1$(CURSCREE%)
 3740 COLOUR 7 :PRINT TAB(1,19) TLINE2$(CURSCREE%)
 3750 ENDPROC
 3760 DEF PROC_DRAWITEMS
 3770 COLOUR 128
 3780 ITEM%=(MAP%(I%,J%,CURSCREE%))-65
 3790 ITEMS%(ITEM%,0)=CURSCREE% : ITEMS%(ITEM%,1)=I% : ITEMS%(ITEM%,2)=J%
 3800 IF ITEMS%(ITEM%,3)=1 THEN COLOUR11:PRINTTAB(I%,J%)CHR$(241)
 3810 IF ITEMS%(ITEM%,3)=2 THEN COLOUR14:PRINTTAB(I%,J%)CHR$(249)
 3820 IF ITEMS%(ITEM%,3)=3 THEN COLOUR13:PRINTTAB(I%,J%)CHR$(249)
 3830 IF ITEMS%(ITEM%,3)=4 THEN COLOUR12:PRINTTAB(I%,J%)CHR$(249)
 3840 IF ITEMS%(ITEM%,3)=5 THEN COLOUR10:PRINTTAB(I%,J%)CHR$(249)
 3850 IF ITEMS%(ITEM%,3)=6 THEN COLOUR11:PRINTTAB(I%,J%)CHR$(244)
 3860 IF ITEMS%(ITEM%,3)=7 THEN COLOUR9:PRINTTAB(I%,J%)CHR$(250)
 3870 IF ITEMS%(ITEM%,3)=8 THEN COLOUR7:PRINTTAB(I%,J%)CHR$(241)
 3880 IF ITEMS%(ITEM%,3)=9 THEN COLOUR3:PRINTTAB(I%,J%)CHR$(239)
 3890 ENDPROC
 3900 DEF PROC_POPMONSTER
 3910 FOR K%=0TO4
 3920   FOR J%=1TO16
 3930     FOR I%=1TO57
 3940       M%=MAP%(I%,J%,K%)
 3950       IF M%<97 OR M%>122 THEN GOTO 3990
 3960       MONSTERS%(M%-97,1)=K% : MONSTERS%(M%-97,2)=I% : MONSTERS%(M%-97,3)=J%
 3970       IF MONSTERS%(M%-97,0)=138 THEN MAP%(I%,J%,K%)=138 GOTO 3990
 3980       MAP%(I%,J%,K%)=MONSTERS%(M%-97,7)
 3990     NEXT
 4000   NEXT
 4010 NEXT
 4020 ENDPROC
 4030 DEF PROC_DRAWMONSTER
 4040 FOR I%=0TO20
 4050   IF MONSTERS%(I%,2)=0 THEN GOTO 4120
 4060   IF MONSTERS%(I%,0)=138 THEN COLOUR15
 4070   IF MONSTERS%(I%,0)=130ORMONSTERS%(I%,0)=134 THEN COLOUR14
 4080   IF MONSTERS%(I%,0)=131ORMONSTERS%(I%,0)=132 THEN COLOUR9
 4090   IF MONSTERS%(I%,0)=132ORMONSTERS%(I%,0)=135 THEN COLOUR3
 4100   IF MONSTERS%(I%,0)=133ORMONSTERS%(I%,0)=137 THEN COLOUR12
 4110   IF MONSTERS%(I%,1)=CURSCREE% PRINTTAB(MONSTERS%(I%,2),MONSTERS%(I%,3))CHR$(MONSTERS%(I%,0))
 4120 NEXT
 4130 ENDPROC
 4140 DEF PROC_FIXMAP
 4150 IF MAP%(OX%,OY%,CURSCREE%)=32 PRINT TAB(OX%,OY%) " " :GOTO 4280
 4160 IF MAP%(OX%,OY%,CURSCREE%)=34 COLOURBG%:PRINT TAB(OX%,OY%) CHR$(230):GOTO 4280
 4170 IF MAP%(OX%,OY%,CURSCREE%)=38 COLOUR8:PRINT TAB(OX%,OY%) CHR$(230):GOTO 4280
 4180 IF MAP%(OX%,OY%,CURSCREE%)=46 COLOUR8:PRINT TAB(OX%,OY%) CHR$(230):GOTO 4280
 4190 IF MAP%(OX%,OY%,CURSCREE%)=35 COLOUR2 :PRINT TAB(OX%,OY%) CHR$(233):GOTO 4280
 4200 IF MAP%(OX%,OY%,CURSCREE%)=91 COLOUR128:COLOUR10 :PRINTTAB(OX%,OY%)CHR$(240):GOTO 4280
 4210 IF MAP%(OX%,OY%,CURSCREE%)=92 COLOUR128:COLOUR11 :PRINTTAB(OX%,OY%)CHR$(240):GOTO 4280
 4220 IF MAP%(OX%,OY%,CURSCREE%)=93 COLOUR128:COLOUR12 :PRINTTAB(OX%,OY%)CHR$(240):GOTO 4280
 4230 IF MAP%(OX%,OY%,CURSCREE%)=94 COLOUR128:COLOUR13 :PRINTTAB(OX%,OY%)CHR$(240):GOTO 4280
 4240 IF MAP%(OX%,OY%,CURSCREE%)=138 COLOUR128:COLOUR15 :PRINTTAB(OX%,OY%)CHR$(138):GOTO 4280
 4250 IF MAP%(OX%,OY%,CURSCREE%)=96 COLOUR128:COLOUR15 :PRINTTAB(OX%,OY%)CHR$(139):GOTO 4280
 4260 IF MAP%(OX%,OY%,CURSCREE%)=37THENCOLOUR128+BGCOL%:COLOURWC%:PRINTTAB(OX%,OY%)CHR$(238)
 4270 IF MAP%(OX%,OY%,CURSCREE%)=36THENCOLOUR128+BGCOL%:COLOURWC%:PRINTTAB(OX%,OY%)CHR$(237)
 4280 ENDPROC
 4290 DEF PROC_NEXTSCREEN
 4300 IF PY%=1 THEN PY%=15 GOTO 4330
 4310 IF PY%=15 AND FALLING%=1 THEN PY%=2
 4320 IF PY%=16 THEN PY%=2
 4330 CLS
 4340 PROC_DRAWBOX
 4350 CURSCREE% = SCREENS%(SCREEX%,SCREEY%)
 4360 PROC_DRAWMAP
 4370 PROC_DRAWMONSTER
 4380 ENDPROC
 4390 DEF PROC_CHECKENEMY
 4400 FOR I%=0TO20
 4410   IF MONSTERS%(I%,1)<>CURSCREE% THEN GOTO 4440
 4420   IF PX%+1=MONSTERS%(I%,2) AND PY%=MONSTERS%(I%,3):PDIREC%=236 :PROC_FIGHTGREP(I%)
 4430   IF PX%-1=MONSTERS%(I%,2) AND PY%=MONSTERS%(I%,3):PDIREC%=235 :PROC_FIGHTGREP(I%)
 4440 NEXT
 4450 ENDPROC
 4460 DEF PROC_CHECKITEM
 4470 FOR I%=0TO25
 4480   IF ITEMS%(I%,0)<>CURSCREE% OR ITEMS%(I%,3)=0 THEN GOTO 4510
 4490   IF PX%+1=ITEMS%(I%,1) AND PY%=ITEMS%(I%,2) PROC_ITEMPICK(I%)
 4500   IF PX%-1=ITEMS%(I%,1) AND PY%=ITEMS%(I%,2) PROC_ITEMPICK(I%)
 4510 NEXT
 4520 ENDPROC
 4530 DEF PROC_ITEMPICK(TEM%)
 4540 COLOUR 15
 4550 TMX%=ITEMS%(TEM%,1) : TMY%=ITEMS%(TEM%,2)
 4560 IF ITEMS%(TEM%,3)=1 THEN PROC_PICKGOLD
 4570 IF ITEMS%(TEM%,3)=4 THEN PROC_PICKPA
 4580 IF ITEMS%(TEM%,3)=3 THEN PROC_PICKHP
 4590 IF ITEMS%(TEM%,3)=2 THEN PROC_PICKPH
 4600 IF ITEMS%(TEM%,3)=5 THEN PROC_PICKPS
 4610 IF ITEMS%(TEM%,3)=6 THEN PROC_PICKKEY
 4620 IF ITEMS%(TEM%,3)=7 THEN PROC_PICKWEAPON
 4630 IF ITEMS%(TEM%,3)=8 THEN PROC_PICKCHEST
 4640 IF ITEMS%(TEM%,3)=9 THEN PROC_PICKWAND
 4650 IF ITEMS%(TEM%,3)=0 AND ITEMS%(TEM%,6)=46 COLOUR8:PRINTTAB(TMX%,TMY%)CHR$(230):MAP%(TMX%,TMY%,CURSCREE%)=46
 4660 IF ITEMS%(TEM%,3)=0 AND ITEMS%(TEM%,6)=34 COLOURBG%:PRINTTAB(TMX%,TMY%)CHR$(230):MAP%(TMX%,TMY%,CURSCREE%)=34
 4670 IF ITEMS%(TEM%,3)=0 AND ITEMS%(TEM%,6)=32 COLOURBG%:PRINTTAB(ITEMS%(TEM%,1),ITEMS%(TEM%,2))" ":MAP%(TMX%,TMY%,CURSCREE%)=32
 4680 PROC_CLEARBATTLE
 4690 ENDPROC
 4700 DEF PROC_PICKWAND
 4710 PRINTTAB(20,24)"You found ";:COLOUR14:PRINT WAND$(ITEMS%(TEM%,4));:COLOUR15:PRINT" Wand"
 4720 PRINTTAB(20,26)"Press [Y] to take wand"
 4730 A$=GET$ : PROC_CLEARNPC
 4740 IF A$<>"Y" THEN GOTO 4800
 4750 PRINTTAB(20,24)"Choose a wand slot [1]..[8]       " : A$=GET$
 4760 A%=VAL(A$)-1
 4770 IF A%<0 OR A%>7 THEN GOTO 4800
 4780 PWANDS%(A%,0)=ITEMS%(TEM%,4):PWANDS%(A%,1)=ITEMS%(TEM%,5)
 4790 ITEMS%(TEM%,3)=0
 4800 ENDPROC
 4810 DEF PROC_PICKCHEST
 4820 PRINTTAB(20,24)"The chest contains ";CHESTITEM$(ITEMS%(TEM%,4))
 4830 PRINTTAB(20,26)"Press [Y] to take item." :A$=GET$:PROC_CLEARNPC
 4840 IF A$<>"Y" THEN GOTO 4890
 4850 PRINTTAB(20,24)"Choose an item slot [1]..[8]  ":A$=GET$:A%=VAL(A$)
 4860 IF A%<1ORA%>8 THEN GOTO 4830
 4870 PLITEMS$(A%-1)=CHESTITEM$(ITEMS%(TEM%,4))
 4880 ITEMS%(TEM%,3)=0
 4890 ENDPROC
 4900 DEF PROC_PICKPA
 4910 PRINTTAB(20,24)"A flask of Increased Ferocity!"
 4920 PRINT TAB(20,26)"Press [Y] to use. "
 4930 A$=GET$ : PROC_CLEARNPC
 4940 IF A$<>"Y" GOTO 5000
 4950 MAXA%=MAXA%+ITEMS%(TEM%,4) : PSTAT%(1)=MAXA%+SWORD%: ITEMS%(TEM%,3)=0
 4960 PRINT TAB(20,24)"Attack has increased by ";ITEMS%(TEM%,4);"      "
 4970 PRINT TAB(20,26)"Press any key to continue.  "
 4980 A$=GET$
 4990 ITEMS%(TEM%,3)=0
 5000 ENDPROC
 5010 DEF PROC_PICKWEAPON
 5020 IF PX%=OX% THEN GOTO 5100
 5030 NSWORD%=ITEMS%(TEM%,4)*ITEMS%(TEM%,5)
 5040 PRINTTAB(20,24)"A sword that does ";NSWORD%;" damage."
 5050 PRINTTAB(20,26)"Press [Y] to take sword."
 5060 A$=GET$
 5070 IF A$<>"Y" THEN GOTO 5100
 5080 PSTAT%(1)=MAXA%+NSWORD% : ITEMS%(TEM%,3)=0
 5090 SWORD%=NSWORD%
 5100 ENDPROC
 5110 DEF PROC_PICKPH
 5120 PRINTTAB(20,24)"A flask of Increased Health!"
 5130 PRINT TAB(20,25)"Press [Y] to take item?"
 5140 A$=GET$:PROC_CLEARNPC
 5150 IF A$<>"Y" GOTO 5210
 5160 MAXH%=MAXH%+ITEMS%(TEM%,4) : ITEMS%(TEM%,3)=0
 5170 PRINT TAB(20,24)"Max Health increased by ";ITEMS%(TEM%,4);"     "
 5180 PRINT TAB(20,26)"Press any key to continue."
 5190 A$=GET$
 5200 PSTAT%(0)=MAXH%
 5210 ENDPROC
 5220 DEF PROC_PICKPS
 5230 PRINTTAB(20,24)"A flask of Increased Haste!"
 5240 PRINT TAB(20,26)"Press [Y] to drink flask?"
 5250 A$=GET$:PROC_CLEARNPC
 5260 IF A$<>"Y" GOTO 5210
 5270 MAXS%=MAXS%+ITEMS%(TEM%,4) : ITEMS%(TEM%,3)=0
 5280 PRINT TAB(20,24)"Max Speed has increased by ";ITEMS%(TEM%,4);
 5290 PRINT TAB(20,26)"Press any key to continue."
 5300 A$=GET$
 5310 PSTAT%(2)=MAXS%
 5320 ENDPROC
 5330 DEF PROC_PICKHP
 5340 PRINTTAB(20,24)"A Health Potion"
 5350 PRINT TAB(20,26)"Press [Y] to take item."
 5360 A$=GET$:PROC_CLEARNPC
 5370 IF A$<>"Y" GOTO 5460
 5380 P%=1
 5390 FOR I%=0TO3
 5400   IF PPOTS%(I%,0)=0 AND P%=1 THEN PPOTS%(I%,0)=1:PPOTS%(I%,1)=ITEMS%(TEM%,4):PPOTS%(I%,2)=ITEMS%(TEM%,5):P%=0:ITEMS%(TEM%,3)=0
 5410 NEXT
 5420 IF P%=1 THEN PRINTTAB(20,24)"Not enough space."
 5430 IF P%=0 THEN PRINT TAB(20,24)"You picked up a Health Potion"
 5440 PRINT TAB(20,26)"Press any key to continue."
 5450 A$=GET$
 5460 P%=0
 5470 ENDPROC
 5480 DEF PROC_PICKGOLD
 5490 PRINTTAB(20,24)"You found ";ITEMS%(TEM%,4)*ITEMS%(TEM%,5);" gold!"
 5500 PRINT TAB(20,26)"Press [Y] to take."
 5510 A$=GET$
 5520 IF A$="Y" GOLD%=GOLD%+ITEMS%(TEM%,4)*ITEMS%(TEM%,5):ITEMS%(TEM%,3)=0
 5530 ENDPROC
 5540 DEF PROC_PICKKEY
 5550 IF ITEMS%(TEM%,4)=1 THEN PRINTTAB(20,24)"You found the Green key"  : KEY%(0)=1:ITEMS%(TEM%,3)=0
 5560 IF ITEMS%(TEM%,4)=2 THEN PRINTTAB(20,24)"You found the Yellow key" : KEY%(1)=1:ITEMS%(TEM%,3)=0
 5570 IF ITEMS%(TEM%,4)=3 THEN PRINTTAB(20,24)"You found the Blue key"   : KEY%(2)=1:ITEMS%(TEM%,3)=0
 5580 IF ITEMS%(TEM%,4)=4 THEN PRINTTAB(20,24)"You found the Magenta key": KEY%(3)=1:ITEMS%(TEM%,3)=0
 5590 PRINTTAB(20,26)"Press any key to continue."
 5600 A$=GET$
 5610 ENDPROC
 5620 DEF PROC_USEWAND
 5630 PRINTTAB(20,24)"Choose a wand to use [1]..[8]" : G$=GET$ : G%=VAL(G$)
 5640 IF G%<1 OR G%>8 THEN GOTO 5990
 5650 IF PWANDS%(G%-1,0)<5 AND PWANDS%(G%-1,0)>0  THEN PRINTTAB(20,26)"Try using this wand in combat" :A$=GET$: GOTO 6000
 5660 IF PWANDS%(G%-1,0)=0 THEN GOTO 5700
 5670 IF PWANDS%(G%-1,0)=5 THEN PROC_TELEPORT
 5680 IF PWANDS%(G%-1,0)=6 THEN PROC_LEVITATE
 5690 GOTO 5710
 5700 PRINTTAB(20,26)"You wave a finger, nothing happens." :A$=GET$
 5710 PROC_CLEARBATTLE
 5720 ENDPROC
 5730 DEF PROC_LEVITATE
 5740 LEVIOSA%=20
 5750 PWANDS%(G%-1,0)=0
 5760 ENDPROC
 5770 DEF PROC_TELEPORT
 5780 PRINTTAB(20,26)"Choose a direction to teleport"
 5790 PRINTTAB(24,28)"[";CHR$(248);"]"
 5800 PRINTTAB(20,29)"[";CHR$(245);"] [";CHR$(246);"] ["CHR$(247);"]":A%=GET:PROC_CLEARNPC
 5810 TELEDIREC%=0
 5820 IF A%=11 THEN TELEDIREC%=1 : REM SDL 139
 5830 IF A%=21 THEN TELEDIREC%=2 : REM SDL 137
 5840 IF A%=10 THEN TELEDIREC%=3 : REM SDL 138
 5850 IF A%=8 THEN TELEDIREC%=4 : REM SDL 136
 5860 IF TELEDIREC%=0 THEN 5990 :REM WAVE TWIG
 5870 PRINTTAB(20,26)"How many spaces to teleport"
 5880 PRINTTAB(20,27)"[2] [4] [6]  ":A$=GET$ :A%=VAL(A$)
 5890 IF A%=2 OR A%=4 OR A%=6 ELSE PRINTTAB(20,28)"The spell does not work":A$=GET$:GOTO 5990
 5900 IF TELEDIREC%=1 OR TELEDIREC%=4 THEN A%=0-A%
 5910 IF TELEDIREC%=2 OR TELEDIREC%=4 THEN GOTO 5930
 5920 IF PY%+A%<2 OR PY%+A%>15 THEN :PWANDS%(G%-1,0)=0:GOTO 5980   :REM GLIMPSE
 5930 IF PX%+A%<2 OR PX%+A%>55 THEN :PWANDS%(G%-1,0)=0:GOTO 5980
 5940 IF TELEDIREC%=1 OR TELEDIREC%=3 THEN PY%=PY%+A%
 5950 IF TELEDIREC%=2 OR TELEDIREC%=4 THEN PX%=PX%+A%
 5960 IF MAP%(PX%,PY%,CURSCREE%)=33 THEN DEATH%=1
 5970 PWANDS%(G%-1,0)=0 : GOTO 6000     :REMCLEAR
 5980 PRINTTAB(20,28)"You catch a glimpse between worlds":PRINTTAB(20,29)"and recoil from the void...":A$=GET$:GOTO 6000 :REM CLEAR
 5990 PRINTTAB(20,26)"You wave a random twig." :A$=GET$
 6000 PROC_CLEARNPC
 6010 ENDPROC
 6020 DEF PROC_FIGHTGREP(BM%)
 6030 IF MONSTERS%(BM%,0)=138 GOTO 6560
 6040 COLOUR 15:COLOUR 128: PRINT TAB(PX%,PY%) CHR$(PDIREC%):PRINT TAB(OX%,OY%)" "
 6050 BATTLE%=1 : BTURN%=0
 6060 PSPD%=PSTAT%(2) : MSPD%=MONSTERS%(BM%,6)
 6070 PTURN%=PSPD% DIV MSPD% : PMOD%=PSPD% MOD MSPD%
 6080 MTURN%=MSPD% DIV PSPD% : MMOD%=MSPD% MOD PSPD%
 6090 TURN%=0 : URN%=0
 6100 REPEAT
 6110   BTURN%=BTURN%+1
 6120   PROC_PMONSTAT
 6130   IF PTURN%>MTURN% THEN TURN%=2
 6140   IF PTURN%<MTURN% THEN TURN%=1
 6150   IF PTURN%=MTURN% AND PMOD%>=MMOD% AND URN%=0 THEN TURN%=2 : URN%=1 : GOTO 6180
 6160   IF PTURN%=MTURN% AND URN%=1 THEN TURN%=1 : URN%=0
 6170   IF TURN%=1 COLOUR3:GOTO 6310
 6180   COLOUR15:PRINTTAB(18,32)"[A]";:COLOUR10:PRINT"ttack ";
 6190   COLOUR15:PRINT"[F]";:COLOUR10:PRINT"lee ";
 6200   COLOUR15:PRINT"[W]";:COLOUR10:PRINT"and ";
 6210   COLOUR15:PRINT"[P]";:COLOUR10:PRINT"otion"
 6220   B$=GET$ : PRINTTAB(18,32)"                                     "
 6230   IF B$="P" THEN PROC_HEALPOT
 6240   IF B$="P" THEN GOTO 6360
 6250   IF B$="F" THEN COLOUR3:PSTAT%(0)=PSTAT%(0)-(MONSTERS%(BM%,5)/2): PRINT TAB(18,27) MONSNAME$(BM%+1)" does ";(MONSTERS%(BM%,5)/2);" damage."
 6260   IF B$="F" THEN PRINTTAB(18,28)"You flee the battle." : BATTLE%=2 :A$=GET$:GOTO 6450
 6270   IF B$="W" THEN PROC_BATTLEWAND
 6280   IF B$="W" THEN GOTO 6360
 6290   PRINT TAB(18,26) "Hero Attacks!"
 6300   MONSTERS%(BM%,4)=MONSTERS%(BM%,4)-PSTAT%(1): PRINT TAB(18,27) "Hero does ";PSTAT%(1);" damage."
 6310   IF TURN%=1 PRINT TAB(18,26) MONSNAME$(BM%+1);" Attacks!"
 6320   IF TURN%=1 PSTAT%(0)=PSTAT%(0)-MONSTERS%(BM%,5): PRINT TAB(18,27) MONSNAME$(BM%+1)" does ";MONSTERS%(BM%,5);" damage."
 6330   COLOUR15:PRINTTAB(18,29)"Press any key"
 6340   A$=GET$:PROC_CLEARBATTLE
 6350   IF MONSTERS%(BM%,4)<=0 THEN MONSTERS%(BM%,4)=0
 6360   PROC_PRINTSTATS : PROC_PMONSTAT
 6370   A%=INKEY(100)
 6380   IF TURN%=1 THEN MTURN%=MTURN%-1
 6390   IF TURN%=2 THEN PTURN%=PTURN%-1
 6400   IF MTURN%<=0 AND PTURN%>0 THEN TURN%=2
 6410   IF PTURN%<=0 AND MTURN%>0 THEN TURN%=1
 6420   IF PTURN%<=0 AND MTURN%<=0 THEN PROC_TURNZERO
 6430   IF MONSTERS%(BM%,4)<=0THEN BATTLE%=3
 6440   IF PSTAT%(0)<=0 THEN BATTLE%=4
 6450 UNTIL BATTLE%<>1
 6460 PROC_CLEARBATTLE
 6470 IF BATTLE%=3 PRINTTAB(18,29)MONSNAME$(BM%+1); " defeated!": MKILLS%=MKILLS%+1
 6480 IF BATTLE%=4 THEN DEATH%=2
 6490 IF BATTLE%=3 AND PDIREC%=235 THEN MONSTERS%(BM%,1)=30 :PRINTTAB(PX%-1,PY%)"x"
 6500 IF BATTLE%=3 AND PDIREC%=236 THEN MONSTERS%(BM%,1)=30 :PRINTTAB(PX%+1,PY%)"x"
 6510 IF BATTLE%=2 THEN OX%=PX%
 6520 IF BATTLE%=2 AND PDIREC%=235 THEN PX%=PX%+1
 6530 IF BATTLE%=2 AND PDIREC%=236 THEN PX%=PX%-1
 6540 IF BATTLE%=2 THEN COLOUR15:PRINTTAB(PX%,PY%)CHR$(PDIREC%):PROC_FIXMAP
 6550 A$=GET$ :PROC_CLEARBATTLE
 6560 IF MONSTERS%(BM%,0)=138 THEN PROC_NPC
 6570 ENDPROC
 6580 DEF PROC_BATTLEWAND
 6590 Z%=0
 6600 IF MONSTERS%(BM%,0)=130 OR MONSTERS%(BM%,0)=134 THEN Z%=1
 6610 IF MONSTERS%(BM%,0)=131 OR MONSTERS%(BM%,0)=135 THEN Z%=2
 6620 IF MONSTERS%(BM%,0)=132 OR MONSTERS%(BM%,0)=136 THEN Z%=3
 6630 IF MONSTERS%(BM%,0)=133 OR MONSTERS%(BM%,0)=137 THEN Z%=4
 6640 PRINTTAB(18,26)"Choose wand to use ";:COLOUR15:PRINT"[1]..[8]":COLOUR10:A$=GET$ : A%=VAL(A$)
 6650 IF A%<1 OR A%>8 THEN GOTO 6890
 6660 IF PWANDS%(A%-1,0)=0 THEN GOTO 6890
 6670 IF PWANDS%(A%-1,0)=1 AND Z%=1 THEN Z%=9
 6680 IF PWANDS%(A%-1,0)=1 AND Z%=2 THEN MONSTERS%(BM%,6)=MONSTERS%(BM%,6)-PWANDS%(A%-1,1):MONSTERS%(BM%,5)=MONSTERS%(BM%,5)-PWANDS%(A%-1,1):Z%=8
 6690 IF PWANDS%(A%-1,0)=1 AND Z%=3 THEN MONSTERS%(BM%,4)=MONSTERS%(BM%,4)-PWANDS%(A%-1,1):Z%=5
 6700 IF PWANDS%(A%-1,0)=1 AND Z%=4 THEN MONSTERS%(BM%,5)=MONSTERS%(BM%,5)-PWANDS%(A%-1,1):Z%=6
 6710 IF PWANDS%(A%-1,0)=2 AND Z%=3 THEN MONSTERS%(BM%,6)=MONSTERS%(BM%,6)-PWANDS%(A%-1,1):Z%=7
 6720 IF PWANDS%(A%-1,0)=2 AND Z%=1 THEN MONSTERS%(BM%,6)=MONSTERS%(BM%,6)-PWANDS%(A%-1,1):MONSTERS%(BM%,5)=MONSTERS%(BM%,5)-PWANDS%(A%-1,1):Z%=8
 6730 IF PWANDS%(A%-1,0)=2 AND Z%=4 THEN MONSTERS%(BM%,4)=MONSTERS%(BM%,4)-PWANDS%(A%-1,1):Z%=5
 6740 IF PWANDS%(A%-1,0)=2 AND Z%=2 THEN Z%=9
 6750 IF PWANDS%(A%-1,0)=3 AND Z%=4 THEN MONSTERS%(BM%,6)=MONSTERS%(BM%,6)-PWANDS%(A%-1,1):Z%=7
 6760 IF PWANDS%(A%-1,0)=3 AND Z%=2 THEN MONSTERS%(BM%,6)=MONSTERS%(BM%,6)-PWANDS%(A%-1,1):MONSTERS%(BM%,5)=MONSTERS%(BM%,5)-PWANDS%(A%-1,1):Z%=8
 6770 IF PWANDS%(A%-1,0)=3 AND Z%=3 THEN Z%=9
 6780 IF PWANDS%(A%-1,0)=3 AND Z%=1 THEN MONSTERS%(BM%,5)=MONSTERS%(BM%,5)-PWANDS%(A%-1,1):Z%=5
 6790 IF PWANDS%(A%-1,0)=4 AND Z%=1 THEN MONSTERS%(BM%,6)=MONSTERS%(BM%,6)-PWANDS%(A%-1,1):MONSTERS%(BM%,5)=MONSTERS%(BM%,5)-PWANDS%(A%-1,1):Z%=8
 6800 IF PWANDS%(A%-1,0)=4 AND Z%=4 THEN Z%=9
 6810 IF PWANDS%(A%-1,0)=4 AND Z%=2 THEN MONSTERS%(BM%,6)=MONSTERS%(BM%,6)-PWANDS%(A%-1,1):Z%=7
 6820 IF PWANDS%(A%-1,0)=4 AND Z%=3 THEN MONSTERS%(BM%,5)=MONSTERS%(BM%,5)-PWANDS%(A%-1,1):Z%=6
 6830 PROC_PMONSTAT
 6840 IF Z%=5 THEN PRINTTAB(18,28)"Monsters Health decreased by ";PWANDS%(A%-1,1)".":A$=GET$:GOTO 6900
 6850 IF Z%=6 THEN PRINTTAB(18,28)"Monsters Attack decreased by ";PWANDS%(A%-1,1)".":A$=GET$:GOTO 6900
 6860 IF Z%=7 THEN PRINTTAB(18,28)"Monsters Speed decreased by ";PWANDS%(A%-1,1)".":A$=GET$:GOTO 6900
 6870 IF Z%=8 THEN PRINTTAB(18,28)"Monsters Attack and Speed decreased by ";PWANDS%(A%-1,1)".":A$=GET$:GOTO 6900
 6880 IF Z%=9 THEN PRINTTAB(18,28)"Monster is resistant to ";:COLOUR14:PRINT WAND$(PWANDS%(A%-1,0)):COLOUR 15:A$=GET$:GOTO 6900
 6890 PRINTTAB(18,28)"You flourish a stick to no effect.":A$=GET$
 6900 PROC_CLEARNPC : PWANDS%(A%-1,0)=0
 6910 ENDPROC
 6920 DEF PROC_TURNZERO
 6930 PSPD%=PSTAT%(2) : MSPD%=MONSTERS%(BM%,6)
 6940 PSPD%=PSPD%+PMOD%:MSPD%=MSPD%+MMOD%
 6950 PMOD%=PSPD% MOD MSPD% : MMOD%=MSPD% MOD PSPD%
 6960 PTURN%=PSPD% DIV MSPD%:MTURN%=MSPD% DIV PSPD%
 6970 ENDPROC
 6980 DEF PROC_CLEARBATTLE
 6990 FOR IBS%=0TO11
 7000   PRINTTAB(17,22+IBS%)"                                            "
 7010 NEXT
 7020 ENDPROC
 7030 DEF PROC_DEATH(PLRIP%)
 7040 CLS : COLOUR9
 7050 PRINTTAB(20,20)"YOU ARE A GHOST"
 7060 IF PLRIP%=1 THEN PRINTTAB(18,22)"YOU TELEPORTED INTO A WALL"
 7070 IF PLRIP%=2 THEN PRINTTAB(18,22)"YOU WERE SLAYED BY A MONSTER"
 7080 IF PLRIP%=3 THEN PRINTTAB(18,22)"YOU WERE IMPALED ON A SPIKE"
 7090 PRINTTAB(PX%,PY%)CHR$(232)
 7100 A$=GET$
 7110 ENDPROC
 7120 DEF PROC_PMONSTAT
 7130 PRINTTAB(18,24)"                                     "
 7140 COLOUR9:PRINTTAB(18,22)"BATTLE: ";:COLOUR15:PRINT MONSNAME$(BM%+1)
 7150 PRINTTAB(19,24)"Health:";MONSTERS%(BM%,4)
 7160 PRINTTAB(29,24)"Attack:";MONSTERS%(BM%,5)
 7170 PRINTTAB(39,24)"Speed:";MONSTERS%(BM%,6)
 7180 COLOUR9:PRINTTAB(50,22)"TURN: ";:COLOUR15:PRINT; BTURN%
 7190 ENDPROC
 7200 DEF PROC_NPC
 7210 PRINTTAB(20,24)"                                        "
 7220 COLOUR11:PRINTTAB(20,24)MONSNAME$(BM%+1);":"
 7230 COLOUR15:NPCTYPE$=LEFT$(MONSNAME$(BM%+1),4)
 7240 NORMY%=0
 7250 IF NPCTYPE$="Shop" THEN NORMY%=1: PROC_NPCSHOP
 7260 IF NPCTYPE$="Ques" THEN NORMY%=1: PROC_NPCQUEST
 7270 IF NORMY%=0 THEN PROC_NPCCHAT
 7280 PROC_CLEARNPC
 7290 PRINTTAB(20,24)"                                          "
 7300 ENDPROC
 7310 DEF PROC_NPCCHAT
 7320 FOR NT%=0 TO 4
 7330   IF NT%=3 THEN COLOUR15 ELSE COLOUR 14
 7340   PRINTTAB(18,26+NT%)NPCSPEAK$(MONSTERS%(BM%,4),NT%)
 7350 NEXT
 7360 A$=GET$ : PROC_CLEARNPC
 7370 ENDPROC
 7380 DEF PROC_NPCQUEST
 7390 IF MONSTERS%(BM%,5)=0 THEN GOTO 7610
 7400 FOR NT%=0 TO 4
 7410   IF NT%=3 THEN COLOUR15 ELSE COLOUR 14
 7420   PRINTTAB(18,26+NT%)NPCSPEAK$(MONSTERS%(BM%,4),NT%)
 7430 NEXT
 7440 A$=GET$ : PROC_CLEARNPC : COLOUR 15
 7450 PRINTTAB(18,26)"Press [I] to give item" : A$=GET$
 7460 IF A$<>"I" THEN PRINTTAB(18,28)"Please if you find it....":A$=GET$:GOTO 7620
 7470 PRINTTAB(18,28)"Select which item to give [1]..[8]" : A$=GET$ :A%=VAL(A$)
 7480 IF A%<1 OR A%>8 THEN PRINTTAB(18,30)"Don't jest with me.." :A$=GET$:GOTO 7620
 7490 IF PLITEMS$(A%-1)<>LEFT$(NPCITEMS$(MONSTERS%(BM%,8)),10) THEN PRINTTAB(18,30)"No that's not it.":A$=GET$:GOTO 7620
 7500 PRINTTAB(18,30)"YOU FOUND IT!" :QUESTS%=QUESTS%+1
 7510 PLITEMS$(A%-1)="          "
 7520 IF MONSTERS%(BM%,5)=6 THEN KEY%(MONSTERS%(BM%,6))=1
 7530 IF MONSTERS%(BM%,5)=5 THEN MAXS%=MAXS%+MONSTERS%(BM%,6) :PSTAT%(2)=MAXS%
 7540 IF MONSTERS%(BM%,5)=4 THEN MAXA%=MAXA%+MONSTERS%(BM%,6) :PSTAT%(1)=MAXA%
 7550 IF MONSTERS%(BM%,5)=3 THEN MAXH%=MAXH%+MONSTERS%(BM%,6) :PSTAT%(0)=MAXH%
 7560 IF MONSTERS%(BM%,5)=10 THEN PROC_REVEALHID
 7570 IF MONSTERS%(BM%,5)<>9 THEN GOTO 7600
 7580 PLITEMS$(A%-1)=RIGHT$(NPCSPEAK$(MONSTERS%(BM%,4),4),10)
 7590 PRINTTAB(18,31)"Here take this ";PLITEMS$(A%-1);" in kind."
 7600 MONSTERS%(BM%,5)=0:A$=GET$:GOTO 7620
 7610 COLOUR15:PRINTTAB(18,26)"Go well hero." :A$=GET$
 7620 PROC_CLEARNPC
 7630 ENDPROC
 7640 DEF PROC_REVEALHID
 7650 PRINTTAB(20,24)"Something is changing."
 7660 IF HID%(CURSCREE%)=1 THEN HID%(CURSCREE%)=0: GOTO 7680
 7670 IF HID%(CURSCREE%)=0 THEN HID%(CURSCREE%)=1
 7680 PROC_DRAWMAP
 7690 PROC_DRAWMONSTER
 7700 PRINTTAB(PX%,PY%)CHR$(PDIREC%)
 7710 PROC_CLEARBATTLE
 7720 ENDPROC
 7730 DEF PROC_NPCSHOP
 7740 IF MONSTERS%(BM%,5)=0 THEN GOTO 8040
 7750 FOR NT%=0 TO 4
 7760   PRINTTAB(18,26+NT%)NPCSPEAK$(MONSTERS%(BM%,4),NT%)
 7770 NEXT
 7780 PRICE$=LEFT$(NPCSPEAK$(MONSTERS%(BM%,4),3),3)
 7790 PRICE%=VAL(PRICE$)
 7800 IF MONSTERS%(BM%,5)=7 THEN ITEM$="SWORD"
 7810 IF MONSTERS%(BM%,5)=3 THEN ITEM$="MAX HEALTH BOOST"
 7820 IF MONSTERS%(BM%,5)=4 THEN ITEM$="DAMAGE BOOST"
 7830 IF MONSTERS%(BM%,5)=5 THEN ITEM$="INCREASED SPEED"
 7840 IF MONSTERS%(BM%,5)=11 THEN ITEM$=RIGHT$(NPCSPEAK$(MONSTERS%(BM%,4),3),10)
 7850 IF MONSTERS%(BM%,5)=12 THEN PROC_SHOPBUY : PROC_CLEARNPC : GOTO 8060
 7860 PRINTTAB(18,32)"Buy "ITEM$" for ";PRICE%;" [Y] or [N]?"
 7870 A$=GET$
 7880 PROC_CLEARNPC
 7890 IF A$<>"Y" THEN PRINTTAB(18,26)"I will wait if you change your mind.     " :A$=GET$:GOTO 8060
 7900 IF GOLD%-PRICE%<0 THEN PRINTTAB(18,26)"You do not have enough gold.      " :A$=GET$:GOTO 8060
 7910 GOLD%=GOLD%-PRICE%
 7920 IF MONSTERS%(BM%,5)=7 THEN PRINTTAB(18,26)"This blade will serve you well." : A$=GET$
 7930 IF MONSTERS%(BM%,5)=7 THEN SWORD%=MONSTERS%(BM%,6) : MONSTERS%(BM%,5)=0 : PSTAT%(1)=MAXA%+SWORD%: GOTO 8060
 7940 IF MONSTERS%(BM%,5)=3 THEN PRINTTAB(18,26)"Use this wisely." : A$=GET$
 7950 IF MONSTERS%(BM%,5)=3 THEN MAXH%=MAXH%+MONSTERS%(BM%,6) : MONSTERS%(BM%,5)=0 : PSTAT%(0)=MAXA%: GOTO 8060
 7960 IF MONSTERS%(BM%,5)=4 THEN PRINTTAB(18,26)"You become more powerful."
 7970 IF MONSTERS%(BM%,5)=4 THEN MAXA%=MAXA%+MONSTERS%(BM%,6) : MONSTERS%(BM%,5)=0 : PSTAT%(1)=MAXA%+SWORD%: A$=GET$: GOTO 8060
 7980 IF MONSTERS%(BM%,5)=5 THEN PRINTTAB(18,26)"You can now do more attacks."
 7990 IF MONSTERS%(BM%,5)=5 THEN MAXS%=MAXS%+MONSTERS%(BM%,6) : MONSTERS%(BM%,5)=0 : PSTAT%(2)=MAXS%: A$=GET$: GOTO 8060
 8000 IF MONSTERS%(BM%,5)=11 THEN PRINTTAB(20,26)"Choose an item slot [1]..[8]  ":A$=GET$:A%=VAL(A$)
 8010 IF A%<1ORA%>8 THEN PRINTTAB(20,27)"You cannot put that there." :GOTO 8000
 8020 PLITEMS$(A%-1)=ITEM$ :MONSTERS%(BM%,5)=0 : PRINTTAB(20,27)"Thanks for the business.": A$=GET$: GOTO 8060 
 8030 PROC_CLEARNPC
 8040 PRINTTAB(18,26)"I have no more wares to sell."
 8050 PRINTTAB(18,27)"Fare well hero." :A$=GET$
 8060 ENDPROC
 8070 DEF PROC_SHOPBUY
 8080 ITEM$=LEFT$(NPCSPEAK$(MONSTERS%(BM%,4),3),10) : PRICE$=RIGHT$(NPCSPEAK$(MONSTERS%(BM%,4),4),3) :PRICE%=VAL(PRICE$)
 8090 PRINTTAB(18,32)"Sell "ITEM$" for ";PRICE%;" [Y] or [N]?" : A$=GET$
 8100 IF A$<>"Y" THEN : PROC_CLEARNPC :GOTO 8150
 8110 PROC_CLEARNPC : PRINTTAB(18,26)"Choose an item to sell [1]...[8]":A$=GET$:A%=VAL(A$)
 8120 IF A%<1 OR A%>8 THEN PRINTTAB(18,28)"I thought you meant business.": A$=GET$: : GOTO 8150
 8130 IF PLITEMS$(A%-1)<>ITEM$ THEN PRINTTAB(18,28)"You insult me. I want "ITEM$ :A$=GET$: GOTO 8150
 8140 GOLD%=GOLD%+PRICE% : PLITEMS$(A%-1)="          " : PRINTTAB(18,26)"I will buy more if you find it." : A$=GET$
 8150 ENDPROC
 8160 DEF PROC_CLEARNPC
 8170 FOR C%=0TO8
 8180   PRINTTAB(18,25+C%)"                                            "
 8190 NEXT
 8200 ENDPROC
 8210 DEF PROC_PRINTSTATS
 8220 COLOUR 11 :COLOUR 128:PRINTTAB(57,7)"GOLD";
 8230 GOLD$=STR$(GOLD%)
 8240 COLOUR 15:PRINTTAB(57,9)GOLD$
 8250 COLOUR 9 :PRINTTAB(57,3)"KILLS";
 8260 MKILL$=STR$(MKILLS%)
 8270 COLOUR 15:PRINTTAB(57,5)MKILL$
 8280 COLOUR 10 :PRINTTAB(57,11)"QUESTS";
 8290 QUESTS$=STR$(QUESTS%)
 8300 COLOUR 15:PRINTTAB(57,13)QUESTS$
 8310 COLOUR 12
 8320 FOR I%=0TO11
 8330   PRINT TAB(2+I%,22) CHR$(230)
 8340 NEXT
 8350 COLOUR 15:COLOUR136 :
 8360 FFSET%=0
 8370 IF PSTAT%(0)>9 THEN FFSET%=1
 8380 IF PSTAT%(0)>99 THEN FFSET%=2
 8390 PSTAT$=STR$(PSTAT%(0))
 8400 PRINT TAB(2,23) "Health     ":PRINTTAB(13-FFSET%,23)PSTAT$
 8410 FFSET%=0
 8420 IF PSTAT%(1)>9 THEN FFSET%=1
 8430 IF PSTAT%(1)>99 THEN FFSET%=2
 8440 PSTAT$=STR$(PSTAT%(1))
 8450 PRINT TAB(2,24) "Attack     ":PRINTTAB(13-FFSET%,24)PSTAT$
 8460 FFSET%=0
 8470 IF PSTAT%(2)>9 THEN FFSET%=1
 8480 IF PSTAT%(2)>99 THEN FFSET%=2
 8490 PSTAT$=STR$(PSTAT%(2))
 8500 PRINT TAB(2,25) "Speed      ":PRINTTAB(13-FFSET%,25)PSTAT$
 8510 COLOUR12
 8520 FOR I%=0TO11
 8530   PRINT TAB(2+I%,26) CHR$(230)
 8540 NEXT
 8550 COLOUR1
 8560 FOR I%=0TO11
 8570   PRINT TAB(2+I%,28) CHR$(230)
 8580 NEXT
 8590 COLOUR 14:COLOUR136
 8600 PRINT TAB(2,29)"[";:COLOUR15:PRINT"P";:COLOUR14:PRINT"]";:COLOUR15:PRINT"otion "
 8610 FOR I%=0TO3
 8620   PRINTTAB(10+I%,29)" "
 8630   IF PPOTS%(I%,0)=1 THEN PRINTTAB(10+I%,29)CHR$(249)
 8640 NEXT
 8650 PRINT TAB(2,30) "Keys    "
 8660 IF KEY%(0)=1 THEN COLOUR10 PRINTTAB(10,30)CHR$(244) ELSE COLOUR15:COLOUR136 PRINTTAB(10,30)" "
 8670 IF KEY%(1)=1 THEN COLOUR11 PRINTTAB(11,30)CHR$(244) ELSE COLOUR15:COLOUR136 PRINTTAB(11,30)" "
 8680 IF KEY%(2)=1 THEN COLOUR12 PRINTTAB(12,30)CHR$(244) ELSE COLOUR15:COLOUR136 PRINTTAB(12,30)" "
 8690 IF KEY%(3)=1 THEN COLOUR13 PRINTTAB(13,30)CHR$(244) ELSE COLOUR15:COLOUR136 PRINTTAB(13,30)" "
 8700 IF SWORD%>9 THEN FFSET%=1
 8710 IF SWORD%>99 THEN FFSET%=2
 8720 SWORD$=STR$(SWORD%)
 8730 COLOUR 15 : PRINT TAB(2,31) "Sword      ":PRINTTAB(13-FFSET%,31)SWORD$
 8740 PRINT TAB(2,32) "            "
 8750 COLOUR1
 8760 FOR I%=0TO11
 8770   PRINT TAB(2+I%,33) CHR$(230)
 8780 NEXT
 8790 COLOUR 131:COLOUR15
 8800 PRINTTAB(4,36)"     ";:COLOUR14:PRINT"[";:COLOUR15:PRINT"I";:COLOUR14:PRINT"]";:COLOUR15:PRINT"tems               "
 8810 COLOUR128
 8820 FOR I%=1TO4
 8830   I$=STR$(I%)
 8840   PRINT TAB(4,37+I%)I$;".";PLITEMS$(I%-1)
 8850 NEXT
 8860 FOR I%=5TO8
 8870   I$=STR$(I%)
 8880   PRINT TAB(18,37+(I%-4))I$;".";PLITEMS$(I%-1)
 8890 NEXT
 8900 COLOUR 130:COLOUR15
 8910 PRINTTAB(32,36)"     ";:COLOUR14:PRINT"[";:COLOUR15:PRINT"W";:COLOUR14:PRINT"]";:COLOUR15:PRINT"ands               "
 8920 COLOUR128
 8930 FOR I%=1TO4
 8940   I$=STR$(I%)
 8950   PRINT TAB(32,37+I%)I$;".";WAND$(PWANDS%(I%-1,0))
 8960 NEXT
 8970 FOR I%=5TO8
 8980   I$=STR$(I%)
 8990   PRINT TAB(46,37+(I%-4))I$;".";WAND$(PWANDS%(I%-1,0))
 9000 NEXT
 9010 ENDPROC
 9020 DEF PROC_HEALPOT
 9030 P%=1 : J%=0
 9040 FOR I%=0TO3
 9050   IF P%=1 AND PPOTS%(I%,0)=1 THEN PSTAT%(0)=PSTAT%(0)+PPOTS%(I%,1)*PPOTS%(I%,2):P%=0:J%=1:PPOTS%(I%,0)=0
 9060 NEXT
 9070 IF PSTAT%(0)>MAXH% AND J%=1 THEN PSTAT%(0)=MAXH%
 9080 IF J%=1 THEN COLOUR15:PRINT TAB(20,26)"You drank a health potion...":GOTO 9100
 9090 PRINTTAB(20,26)"You do not have any potions."
 9100 A$=GET$ :PROC_CLEARBATTLE
 9110 ENDPROC
 9120 DEF PROC_MOVEL
 9130 PMOVX%=-1
 9140 IF PX%=1 THEN PMOVX%=0
 9150 IF MAP%(PX%-1,PY%,CURSCREE%)=38 AND HID%(CURSCREE%)=0 THEN PMOVX%=0
 9160 IF MAP%(PX%-1,PY%,CURSCREE%)=37 THEN PMOVY%=-1
 9170 IF MAP%(PX%-1,PY%,CURSCREE%)=33 THEN PMOVX%=0 :GOTO 9230
 9180 IF MAP%(PX%-1,PY%,CURSCREE%)>=65 AND MAP%(PX%-1,PY%,CURSCREE%)=<90 THEN PMOVX%=0 :GOTO 9230
 9190 IF MAP%(PX%-1,PY%,CURSCREE%)=91 AND KEY%(0)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVX%=0 :GOTO 9230
 9200 IF MAP%(PX%-1,PY%,CURSCREE%)=92 AND KEY%(1)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVX%=0 :GOTO 9230
 9210 IF MAP%(PX%-1,PY%,CURSCREE%)=93 AND KEY%(2)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVX%=0 :GOTO 9230
 9220 IF MAP%(PX%-1,PY%,CURSCREE%)=94 AND KEY%(3)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVX%=0
 9230 PDIREC%=232
 9240 ENDPROC
 9250 DEF PROC_MOVER
 9260 PMOVX%=1
 9270 IF PX%=55 THEN PMOVX%=0
 9280 IF MAP%(PX%+1,PY%,CURSCREE%)=38 AND HID%(CURSCREE%)=0 THEN PMOVX%=0
 9290 IF MAP%(PX%+1,PY%,CURSCREE%)=36 THEN PMOVY%=-1
 9300 IF MAP%(PX%+1,PY%,CURSCREE%)=33 THEN PMOVX%=0 : GOTO 9360
 9310 IF MAP%(PX%+1,PY%,CURSCREE%)>=65 AND MAP%(PX%+1,PY%,CURSCREE%)=<90 THEN PMOVX%=0 : GOTO 9360
 9320 IF MAP%(PX%+1,PY%,CURSCREE%)=91 AND KEY%(0)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$: PMOVX%=0 :GOTO 9360
 9330 IF MAP%(PX%+1,PY%,CURSCREE%)=92 AND KEY%(1)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVX%=0 :GOTO 9360
 9340 IF MAP%(PX%+1,PY%,CURSCREE%)=93 AND KEY%(2)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVX%=0 :GOTO 9360
 9350 IF MAP%(PX%+1,PY%,CURSCREE%)=94 AND KEY%(3)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVX%=0
 9360 PDIREC%=234
 9370 ENDPROC
 9380 DEF PROC_MOVEU
 9390 PMOVY%=-1
 9400 IF PY%=1 THEN PMOVY%=0
 9410 IF MAP%(PX%,PY%-1,CURSCREE%)=38 AND HID%(CURSCREE%)=0 THEN PMOVY%=0
 9420 IF MAP%(PX%,PY%-1,CURSCREE%)=33 THEN PMOVY%=0
 9430 IF MAP%(PX%,PY%-1,CURSCREE%)>=65 AND MAP%(PX%,PY%-1,CURSCREE%)=<90 THEN PMOVY%=0
 9440 IF MAP%(PX%,PY%-1,CURSCREE%)=91 AND KEY%(0)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$: PMOVY%=0 :GOTO 9480
 9450 IF MAP%(PX%,PY%-1,CURSCREE%)=92 AND KEY%(1)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVY%=0 :GOTO 9480
 9460 IF MAP%(PX%,PY%-1,CURSCREE%)=93 AND KEY%(2)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVY%=0 :GOTO 9480
 9470 IF MAP%(PX%,PY%-1,CURSCREE%)=94 AND KEY%(3)=0 THEN PRINTTAB(20,24)"LOCKED":A$=GET$:PMOVY%=0
 9480 ENDPROC
 9490 DEF PROC_MOVED
 9500 PMOVY%=1
 9510 IF PY%=16 THEN PMOVY%=0
 9520 IF MAP%(PX%,PY%+1,CURSCREE%)=33 THEN PMOVY%=0
 9530 IF MAP%(PX%,PY%+1,CURSCREE%)=38 AND HID%(CURSCREE%)=0 THEN PMOVY%=0
 9540 IF MAP%(PX%,PY%+1,CURSCREE%)>=65 AND MAP%(PX%,PY%+1,CURSCREE%)=<90 THEN PMOVY%=0:GOTO 9590
 9550 IF MAP%(PX%,PY%+1,CURSCREE%)=91 AND KEY%(0)=0 THEN PMOVY%=0 :GOTO 9590
 9560 IF MAP%(PX%,PY%+1,CURSCREE%)=92 AND KEY%(1)=0 THEN PMOVY%=0 :GOTO 9590
 9570 IF MAP%(PX%,PY%+1,CURSCREE%)=93 AND KEY%(2)=0 THEN PMOVY%=0 :GOTO 9590
 9580 IF MAP%(PX%,PY%+1,CURSCREE%)=94 AND KEY%(3)=0 THEN PMOVY%=0
 9590 ENDPROC
 9600 DEF PROC_REALM3D
 9610 COLOUR RND(14):PRINTTAB(6,6)"       ___           ___           ___           ___       ___     "
 9620 COLOUR RND(14):PRINTTAB(6,7)"      /\  \         /\  \         /\  \         /\__\     /\__\    "
 9630 COLOUR RND(14):PRINTTAB(6,8)"     /::\  \       /::\  \       /::\  \       /:/  /    /::|  |   "
 9640 COLOUR RND(14):PRINTTAB(6,9)"    /:/\:\  \     /:/\:\  \     /:/\:\  \     /:/  /    /:|:|  |   "
 9650 COLOUR RND(14):PRINTTAB(6,10)"   /::\~\:\  \   /::\~\:\  \   /::\~\:\  \   /:/  /    /:/|:|__|__ "
 9660 COLOUR RND(14):PRINTTAB(6,11)"  /:/\:\ \:\__\ /:/\:\ \:\__\ /:/\:\ \:\__\ /:/__/    /:/ |::::\__\"
 9670 COLOUR RND(14):PRINTTAB(6,12)"  \/_|::\/:/  / \:\~\:\ \/__/ \/__\:\/:/  / \:\  \    \/__/~~/:/  /"
 9680 COLOUR RND(14):PRINTTAB(6,13)"     |:|::/  /   \:\ \:\__\        \::/  /   \:\  \         /:/  / "
 9690 COLOUR RND(14):PRINTTAB(6,14)"     |:|\/__/     \:\ \/__/        /:/  /     \:\  \       /:/  /  "
 9700 COLOUR RND(14):PRINTTAB(6,15)"     |:|  |        \:\__\         /:/  /       \:\__\     /:/  /   "
 9710 COLOUR RND(14):PRINTTAB(6,16)"      \|__|         \/__/         \/__/         \/__/     \/__/    "
 9720 COLOUR 15
 9730 PRINTTAB(34,43)"                       /^\             "
 9740 PRINTTAB(34,44)"                       |-|             "
 9750 PRINTTAB(34,45)"                  /^\  | |             "
 9760 PRINTTAB(34,46)"           /^\  / [_] \+-+             "
 9770 PRINTTAB(34,47)"          |---||-------| |             "
 9780 PRINTTAB(34,48)" _/^\_    _/^\_|  [_]  |_/^\_   _/^\_  "
 9790 PRINTTAB(34,49)" |___|    |___||_______||___|   |___|  "
 9800 PRINTTAB(34,50)"  | |======| |===========| |=====| |   "
 9810 PRINTTAB(34,51)"  | |      | |    /^\    | |     | |   "
 9820 PRINTTAB(34,52)"  | |      | |   |   |   | |     | |   "
 9830 PRINTTAB(34,53)"  |_|______|_|__ |   |___|_|_____|_|   "
 9840 ENDPROC
